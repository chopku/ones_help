Функция КодировкаФайла(ИмяВременногоФайла, ДанныеФайла)
	
	Результат = Неопределено;
	
	Если ТипЗнч(ДанныеФайла) <> Тип("ДвоичныеДанные") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Буфер = Новый БуферДвоичныхДанных(4);
	Поток = ДанныеФайла.ОткрытьПотокДляЧтения();
	Поток.Прочитать(Буфер, 0, 4);
	// Если 60, то это символ <, тогда ищем кодировку в теге encoding в первой строке xml файла
	Если Буфер[0] = 60 Тогда
		Читатель = Новый ЧтениеТекста(ИмяВременногоФайла);
		ПерваяСтрока = Читатель.ПрочитатьСтроку();
		ШаблонПоиска = "encoding=[\""']([^\""']+)[\""']";
		РезультатПоиска = СтрНайтиПоРегулярномуВыражению(ПерваяСтрока, ШаблонПоиска);
		Если РезультатПоиска.Длина > 0 Тогда
			СписокГрупп = РезультатПоиска.ПолучитьГруппы();
			Если СписокГрупп.Количество() Тогда
				Результат = СписокГрупп[0].Значение;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Буфер[0] = 239 И Буфер[1] = 187 И Буфер[2] = 191 Тогда
		Результат = "UTF-8";
	ИначеЕсли Буфер[0] = 0 И Буфер[1] = 0 И Буфер[2] = 254 И Буфер[3] = 255 Тогда
		Результат = "UTF-32BE";
	ИначеЕсли Буфер[0] = 255 И Буфер[1] = 254 И Буфер[2] = 0 И Буфер[3] = 0 Тогда
		Результат = "UTF-32LE";
	ИначеЕсли Буфер[0] = 254 И Буфер[1] = 255 Тогда
		Результат = "UTF-16BE";
	ИначеЕсли Буфер[0] = 255 И Буфер[1] = 254 Тогда
		Результат = "UTF-16LE";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
